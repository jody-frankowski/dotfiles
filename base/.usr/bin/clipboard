#!/bin/zsh

die() {
    echo "$@" >&2
    exit 1
}

if [[ "$1" == -h || "$1" == --help ]]; then
    echo "\
Usage: clipboard [--paste]  Print the content of the clipboard to stdout
       clipboard TEXT       Copy the first argument to the clipboard
       CMD | clipboard      Copy stdin to the clipboard
       clipboard -h|--help  Print the help message\
" >&2
    exit 1
fi

if _onmacos; then
    copy_cmd=(  pbcopy  )
    paste_cmd=( pbpaste )
else
    if [[ -n $WAYLAND_DISPLAY ]]; then
        copy_cmd=(  wl-copy     )
        paste_cmd=( wl-paste -n )
    elif [[ -n $DISPLAY ]]; then
        if which xclip > /dev/null; then
            copy_cmd=(  xclip -selection clipboard    )
            paste_cmd=( xclip -selection clipboard -o )
        elif which xsel > /dev/null; then
            copy_cmd=(  xsel -bi )
            paste_cmd=( xsel -bo )
        else
            die Error: xclip or xsel not available
        fi
    elif [[ -n $TMUX ]]; then
        echo No Wayland or X11 display detected. Falling back to tmux. >&2

        copy_cmd=(  tmux load-buffer - )
        paste_cmd=( tmux show-buffer   )
    else
        die Error: No tmux, Wayland or X11 system detected
    fi
fi

if [[ "$1" == --paste ]]; then
    exec "${paste_cmd[@]}"
elif [[ -n "$1" ]]; then
    echo -n $1 | "${copy_cmd[@]}"
elif [[ ! -t 0 ]]; then
    # /!\ Do not use cat /dev/stdin as this won't work!
    exec "${copy_cmd[@]}"
else
    exec "${paste_cmd[@]}"
fi
